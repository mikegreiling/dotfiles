# Load our common env exports
source ~/.config/exports;

# --- Determine if we should load the full custom prompt ---
# This is set to false for non-interactive shells like the one used by LLM
# agents (Claude, Cursor, etc.) to prevent prompt decorations from interfering
# with command completion tracking. We use a variable to avoid reordering the whole file.
_zsh_load_custom_prompt=true
if _is_llm_agent; then
  _zsh_load_custom_prompt=false
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ "$_zsh_load_custom_prompt" == true ]]; then
  if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
  fi
fi

# ZSH options
setopt interactive_comments   # allow comments in interactive shell

# History options
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_find_no_dups      # do not display adjacent duplicates within history
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_verify            # show command with history expansion to user before running it
setopt share_history          # share command history data across terminals

# Increase history size to allow 32³ entries
export HISTFILE="$HOME/.zsh_history"
export HISTSIZE=32768
export SAVEHIST=32768

# Load our custom aliases and functions
source ~/.config/aliases;
source ~/.config/functions;
source ~/.config/zsh/functions.zsh;

# Load homebrew-provided autocompletions and zsh plugins
if type brew &>/dev/null; then
  HOMEBREW_PREFIX="$(brew --prefix)";

  # https://docs.brew.sh/Shell-Completion#configuring-completions-in-zsh
  FPATH="${HOMEBREW_PREFIX}/share/zsh/site-functions:${FPATH}";

  autoload -Uz compinit;
  compinit;

  # Load zdharma-continuum/fast-syntax-highlighting
  if [[ -r "$HOMEBREW_PREFIX/share/zsh-fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh" ]]; then
    source $HOMEBREW_PREFIX/share/zsh-fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
  fi

  # Load zsh-autosuggestions
  if [[ -r "$HOMEBREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh" ]]; then
    source $HOMEBREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh

    ZSH_AUTOSUGGEST_STRATEGY=(history completion)
    bindkey '\t' autosuggest-accept # tab
    bindkey '^[[Z' expand-or-complete # shift+tab

    # remove forward-char, vi-forward-char from accept choices, use as partial instead
    export ZSH_AUTOSUGGEST_ACCEPT_WIDGETS=(
      end-of-line
      vi-end-of-line
      vi-add-eol
    )
    ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS+=(forward-char vi-forward-char)
  fi

  # Load zsh-history-substring-search (must come after syntax-highlighting)
  if [[ -r "$HOMEBREW_PREFIX/share/zsh-history-substring-search/zsh-history-substring-search.zsh" ]]; then
    source $HOMEBREW_PREFIX/share/zsh-history-substring-search/zsh-history-substring-search.zsh

    # Limit matches to those which start with the current input
    HISTORY_SUBSTRING_SEARCH_PREFIXED=1

    # Improve highlighting readability
    HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND="bg=cyan,fg=16,bold"
    HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND="bg=red,fg=16,bold"

    # Bind to up and down arrow keys
    bindkey '^[[A' history-substring-search-up
    bindkey '^[[B' history-substring-search-down
  fi

  # Load powerlevel10k prompt
  if [[ "$_zsh_load_custom_prompt" == true ]]; then
    if [[ -r "$HOMEBREW_PREFIX/share/powerlevel10k/powerlevel10k.zsh-theme" ]]; then
      source $HOMEBREW_PREFIX/share/powerlevel10k/powerlevel10k.zsh-theme

      [[ ! -f ~/.config/zsh/p10k.zsh ]] || source ~/.config/zsh/p10k.zsh
    fi
  fi
fi

# Load per-directory-history plugin
HISTORY_BASE="${XDG_CACHE_HOME:-$HOME/.cache}/.directory_history"
PER_DIRECTORY_HISTORY_PRINT_MODE_CHANGE=false
source ~/.config/zsh/per-directory-history.zsh

# Ensure the p10k prompt gets refreshed when toggling local/global history
function toggle-per-directory-history {
  per-directory-history-toggle-history
  refresh-prompt
}
zle -N toggle-per-directory-history
bindkey ${PER_DIRECTORY_HISTORY_TOGGLE:-'^G'} toggle-per-directory-history

# Ensure our git alias has the same autocompletion
[[ "${+functions[compdef]}" -ne 0 ]] && compdef g=git

# Replace cd command with zoxide for smart directory jumping
# Disabled for LLM agents as zoxide's __zoxide_z function causes "command not found" errors
## TODO: consider rewriting __zoxide_z_complete to fix autocomplete behavior
if ! _is_llm_agent && [[ -x "$(command -v zoxide)" ]]; then
  eval "$(zoxide init --cmd cd zsh)"
fi

# Load asdf shims
[ -s "/opt/homebrew/opt/asdf/libexec/asdf.sh" ] && \. "/opt/homebrew/opt/asdf/libexec/asdf.sh"

# Format zsh completion menu
local char_arrow="›"
local char_newline=$'\n'

setopt menu_complete
setopt auto_list

local completion_descriptions="${char_newline}%B%F{85} ${char_arrow} %f%%F{green}%d%b%f"
local completion_warnings="${char_newline}%F{yellow} ${char_arrow} %fno matches for %F{green}%d%f"
local completion_error="${char_newline}%B%F{red} ${char_arrow} %f%e %d error"

zstyle ':completion:*' completer _expand _complete _ignored _approximate
zstyle ':completion:*' use-cache o/n
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/.zcompcache"
zstyle ':completion:*' verbose yes
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list "m:{a-z}={A-Z}"
zstyle ':completion:*' group-name ''

zstyle ':completion:*:*:*:*:descriptions' format $completion_descriptions
zstyle ':completion:*:*:*:*:corrections' format $completion_error
zstyle ':completion:*:*:*:*:default' list-colors ${(s.:.)LS_COLORS} "ma=38;5;253;48;5;23"
zstyle ':completion:*:*:*:*:warnings' format $completion_warnings
zstyle ':completion:*:*:*:*:messages' format "%d"

zstyle ':completion:*:expand:*' tag-order all-expansions
zstyle ':completion:*:approximate:*' max-errors "reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) numeric )"
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters
zstyle ':completion:*:functions' ignored-patterns "_*"

zstyle -e ':completion:*:(ssh|scp|sftp|rsh|rsync):hosts' hosts 'reply=(${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) /dev/null)"}%%[# ]*}//,/ })'

# Load claude (if installed)
if [[ -f "$HOME/.claude/local/claude" ]]; then
  alias claude="~/.claude/local/claude"
fi
